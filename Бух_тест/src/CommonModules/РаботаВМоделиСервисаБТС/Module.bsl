#Область СлужебныйПрограммныйИнтерфейс

// См. описание этой же процедуры в модуле СтандартныеПодсистемыСервер.
Процедура ПриДобавленииОбработчиковСлужебныхСобытий(КлиентскиеОбработчики, СерверныеОбработчики) Экспорт
	
	// СЕРВЕРНЫЕ ОБРАБОТЧИКИ.
	
	СерверныеОбработчики["ТехннологияСервиса.БазоваяФункциональность\ПриФормированииМанифестаКонфигурации"].Добавить(
		"РаботаВМоделиСервисаБТС");
	
КонецПроцедуры

// Вызывается при формировании манифеста конфигурации.
//
// Параметры:
//  РасширенныеСведения - Массив, внутри процедуры обработчика в данный массив требуется
//    добавить объекты типа ОбъектXDTO с ТипомXDTO, унаследованным от
//    {http://www.1c.ru/1cFresh/Application/Manifest/a.b.c.d}ExtendedInfoItem.
//
Процедура ПриФормированииМанифестаКонфигурации(РасширенныеСведения) Экспорт
	
	Если ТранзакцияАктивна() Тогда
		ВызватьИсключение НСтр("ru='Операция не может быть выполнена при активной внешней транзакции!';uk='Операція не може бути виконана при активній зовнішній транзакції!'");
	КонецЕсли;
	
	ВызовВНеразделеннойИБ = Не ОбщегоНазначенияПовтИсп.РазделениеВключено();
	
	НачатьТранзакцию();
	
	Попытка
		
		ОписаниеРазрешений = ФабрикаXDTO.Создать(
			ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Application/Permissions/Manifes/1.0.0.1", "RequiredPermissions")
		);
		
		ОписаниеВнешнихКомпонент = ФабрикаXDTO.Создать(
			ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Application/Permissions/Manifes/1.0.0.1", "Addins")
		);
		
		МакетыВнешнихКомпонент = Новый Соответствие();
		
		Если ВызовВНеразделеннойИБ Тогда
			
			Константы.ИспользоватьРазделениеПоОбластямДанных.Установить(Истина);
			ОбновитьПовторноИспользуемыеЗначения();
			
		КонецЕсли;
		
		Константы.ИспользуютсяПрофилиБезопасности.Установить(Истина);
		Константы.АвтоматическиНастраиватьРазрешенияВПрофиляхБезопасности.Установить(Истина);
		
		ИдентификаторыЗапросов = РаботаВБезопасномРежимеСлужебный.ЗапросыНаОбновлениеРазрешенийКонфигурации();
		
		Дельта = Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсов.ДельтаИзмененийРазрешенийНаИспользованиеВнешнихРесурсов(ИдентификаторыЗапросов);
		
		Для Каждого ЭлементДельты Из Дельта Цикл
			
			Если ЭлементДельты.ВнешнийМодуль = РаботаВБезопасномРежимеСлужебныйПовтИсп.СлужебныйИОМ() Тогда
				
				Таблицы = ЭлементДельты.Изменения.Добавляемые;
				
				Для Каждого Таблица Из Таблицы Цикл
					
					Тип = Таблица.Ключ;
					РазрешенияВТаблице = Таблица.Значение;
					
					Для Каждого РазрешениеВТаблице Из РазрешенияВТаблице Цикл
						
						ОписаниеРазрешения = ФабрикаXDTO.Создать(РаботаВБезопасномРежимеСлужебный.ПакетXDTOПредставленийРазрешений(), Тип);
						
						Если Тип = "FileSystemAccess" Тогда
							ОписаниеРазрешения.Path = РазрешениеВТаблице.Адрес;
							ОписаниеРазрешения.AllowedRead = РазрешениеВТаблице.РазрешеноЧтениеДанных;
							ОписаниеРазрешения.AllowedWrite = РазрешениеВТаблице.РазрешенаЗаписьДанных;
						ИначеЕсли Тип = "CreateComObject" Тогда
							ОписаниеРазрешения.ProgId = РазрешениеВТаблице.ProgId;
							ОписаниеРазрешения.CLSID = РазрешениеВТаблице.CLSID;
							ОписаниеРазрешения.ComputerName = РазрешениеВТаблице.ИмяКомпьютера;
						ИначеЕсли Тип = "AttachAddin" Тогда
							ОписаниеРазрешения = Неопределено;
							ОписанияФайлов = МакетыВнешнихКомпонент.Получить(РазрешениеВТаблице.ИмяМакета);
							Если ОписанияФайлов = Неопределено Тогда
								ОписанияФайлов = Новый Массив();
							КонецЕсли;
							ОписаниеФайла = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Application/Permissions/Manifes/1.0.0.1", "AddinFile"));
							ОписаниеФайла.FileName = РазрешениеВТаблице.ИмяФайла;
							ОписаниеФайла.Hash = РазрешениеВТаблице.ХэшСумма;
							ОписанияФайлов.Добавить(ОписаниеФайла);
							МакетыВнешнихКомпонент.Вставить(РазрешениеВТаблице.ИмяМакета, ОписанияФайлов);
						ИначеЕсли Тип = "RunApplication" Тогда
							ОписаниеРазрешения.CommandMask = РазрешениеВТаблице.ШаблонСтрокиЗапуска;
						ИначеЕсли Тип = "InternetResourceAccess" Тогда
							ОписаниеРазрешения.Protocol = РазрешениеВТаблице.Протокол;
							ОписаниеРазрешения.Host = РазрешениеВТаблице.Адрес;
							ОписаниеРазрешения.Port = РазрешениеВТаблице.Порт;
						КонецЕсли;
						
						Если ОписаниеРазрешения <> Неопределено Тогда
							ОписаниеРазрешений.Permission.Добавить(ОписаниеРазрешения);
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
				Для Каждого КлючИЗначение Из МакетыВнешнихКомпонент Цикл
					
					ИмяМакета = КлючИЗначение.Ключ;
					ОписанияФайлов = КлючИЗначение.Значение;
					
					ОписаниеРазрешения = ФабрикаXDTO.Создать(РаботаВБезопасномРежимеСлужебный.ПакетXDTOПредставленийРазрешений(), "AttachAddin");
					ОписаниеРазрешения.TemplateName = ИмяМакета;
					ОписаниеРазрешений.Permission.Добавить(ОписаниеРазрешения);
					
					ОписаниеВнешнейКомпоненты = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Application/Permissions/Manifes/1.0.0.1", "AddinBundle"));
					ОписаниеВнешнейКомпоненты.TemplateName = ИмяМакета;
					Для Каждого ОписаниеФайла Из ОписанияФайлов Цикл
						ОписаниеВнешнейКомпоненты.Files.Добавить(ОписаниеФайла);
					КонецЦикла;
					ОписаниеВнешнихКомпонент.Bundles.Добавить(ОписаниеВнешнейКомпоненты);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		РасширенныеСведения.Добавить(ОписаниеРазрешений);
		РасширенныеСведения.Добавить(ОписаниеВнешнихКомпонент);
		
	Исключение
		
		ОтменитьТранзакцию();
		Если ВызовВНеразделеннойИБ Тогда
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	ОтменитьТранзакцию();
	Если ВызовВНеразделеннойИБ Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти