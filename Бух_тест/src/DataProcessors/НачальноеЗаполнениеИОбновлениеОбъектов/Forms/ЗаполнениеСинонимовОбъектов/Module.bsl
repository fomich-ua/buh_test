////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЯзыкИнформационнойБазы = Локализация.КодЯзыкаИнформационнойБазы();
	
	КешМетаданныхДерево = Обработки.НачальноеЗаполнениеИОбновлениеОбъектов.ПолучитьДеревоОбъектовСНачальнымЗаполнением(Истина);
	
	СписокВыбора = Элементы.ТипОбъекта.СписокВыбора;
	СписокВыбора.Добавить("ВсеОбъекты", НСтр("ru='Все объекты';uk=""Всі об'єкти"""));
	Для каждого СтрокаОписанияРазделаМетаданных Из КешМетаданныхДерево.Строки Цикл
		СписокВыбора.Добавить(СтрокаОписанияРазделаМетаданных.Имя, СтрокаОписанияРазделаМетаданных.Представление);
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(КешМетаданныхДерево, "КешМетаданных");
	
	ОбработатьУстановкуТипаОбъекта();
	
	УстановитьДоступность(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	
	ОбработатьУстановкуТипаОбъекта();
	
	УстановитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеОбъектаПриИзменении(Элемент)
	
	УстановитьДоступность(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьФормуОбъекта(Команда)
	
	ОткрытьФорму(ТипОбъекта+"."+НаименованиеОбъекта+".ФормаСписка");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступность(ЭтаФорма)
	
	НеобходимоУказатьКонкретныйОбъект = ЗначениеЗаполнено(ЭтаФорма.ТипОбъекта) И ЭтаФорма.ТипОбъекта <> "ВсеОбъекты";
	УказанКонкретныйОбъект            = НеобходимоУказатьКонкретныйОбъект И ЗначениеЗаполнено(ЭтаФорма.НаименованиеОбъекта);
	
	ТипОбъектаСодержитСправочник = ЭтаФорма.ТипОбъекта = "Справочник" ИЛИ ЭтаФорма.ТипОбъекта = "ВсеОбъекты";
	
	Элементы = ЭтаФорма.Элементы;
	
	Элементы.Заполнить.Доступность = УказанКонкретныйОбъект ИЛИ (ЭтаФорма.ТипОбъекта = "ВсеОбъекты");
	Элементы.Открыть.Доступность   = УказанКонкретныйОбъект;
	
	Элементы.НаименованиеОбъекта.Доступность = НеобходимоУказатьКонкретныйОбъект;	
	
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьУстановкуТипаОбъекта()
	
	КешМетаданныхДерево = РеквизитФормыВЗначение("КешМетаданных");
	
	НаименованиеОбъекта = "";
		
	СписокВыбора = Элементы.НаименованиеОбъекта.СписокВыбора;
	СписокВыбора.Очистить();
	Если ЗначениеЗаполнено(ЭтаФорма.ТипОбъекта) И ЭтаФорма.ТипОбъекта <> "ВсеОбъекты" Тогда
		СтрокаДереваСНужнымРазделом = КешМетаданныхДерево.Строки.Найти(ЭтаФорма.ТипОбъекта, "Имя", Ложь);
		Для каждого ИмяОбъектаМетаданных из СтрокаДереваСНужнымРазделом.Строки Цикл
			СписокВыбора.Добавить(ИмяОбъектаМетаданных.Имя, ИмяОбъектаМетаданных.Представление);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Если ТипОбъекта = "ВсеОбъекты" Тогда
		Обработки.НачальноеЗаполнениеИОбновлениеОбъектов.ЗаполнитьВсеОбъекты(Ложь, Истина, Ложь,Истина);
	Иначе	
		Обработки.НачальноеЗаполнениеИОбновлениеОбъектов.ЗаполнитьОбъект(ТипОбъекта, НаименованиеОбъекта, Ложь, Истина, Ложь,Истина);
	КонецЕсли;	
	
Конецпроцедуры


	
