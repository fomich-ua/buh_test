#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура заполняет табличную часть списком валют. В список попадают только 
// валюты, курс которых не зависит от курса других валют.
//
Процедура ЗаполнитьСписокВалют() Экспорт
	
	СписокВалют.Очистить();
	
	ЗагружаемыеВалюты = РаботаСКурсамиВалют.ПолучитьМассивЗагружаемыхВалют();
	
	Для Каждого ЭлементВалюта Из ЗагружаемыеВалюты Цикл
		НоваяСтрока = СписокВалют.Добавить();
		НоваяСтрока.КодВалюты = ЭлементВалюта.Код;
		НоваяСтрока.Валюта    = ЭлементВалюта;
	КонецЦикла;
	
КонецПроцедуры

// Процедура для каждой загружаемой валюты запрашивает файл с курсами
// После загрузки, курсы, удовлетворяющие периоду записываются в регистр сведений
//
Функция ЗагрузитьКурсыВалют(ПриЗагрузкеВозниклиОшибки = Ложь) Экспорт
	
	Возврат РаботаСКурсамиВалютКлиентСервер.ЗагрузитьКурсыВалютПоПараметрам(
		СписокВалют,
		НачалоПериодаЗагрузки,
		ОкончаниеПериодаЗагрузки,
		,,,
		ПриЗагрузкеВозниклиОшибки);
	
КонецФункции


// Производит загрузку курсов и кратностей валют с сайта finance.ua по ежедневным настройкам
//
Функция КодДоступаАктуален() Экспорт
	
	ЗначениеКонстанты = Константы.НастройкиЗагрузкиКурсовВалют.Получить();
	Настройки 		  = ЗначениеКонстанты.Получить();
	
	Если НЕ ЗначениеЗаполнено(Настройки) ИЛИ ПустаяСтрока(Настройки.КодДоступа) Тогда
 		Возврат Ложь;
	КонецЕсли;
	
	КодДоступа = Настройки.КодДоступа;
	
	// Пытаемся прочитать вчерашний курс доллара
	ПроверяемаяДата = ТекущаяДата() - 60*60*24;
	
	СерверИсточник = "fin.1c.ua";
	Адрес = "1c/";
	ТМП   = Формат(ПроверяемаяДата, "ДФ=/yyyy/MM/dd");
	Код = "?" + КодДоступа;
	ФайлНаВебСервере = "http://" + СерверИсточник + "/" + Адрес + "840" + ТМП + ".tsv" + СокрЛП(Код);	
	
	#Если Клиент Тогда
		Результат = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(ФайлНаВебСервере);
	#Иначе
		Результат = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(ФайлНаВебСервере);
	#КонецЕсли
	
	Возврат Результат.Статус;
	
КонецФункции // ПроверитьАктуальностьКодаДоступа()

#КонецЕсли

