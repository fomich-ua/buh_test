////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ОтКого") Тогда 
		ОтКого = Параметры.ОтКого;
	КонецЕсли;
	
	Если Параметры.Свойство("Текст") Тогда 
		Текст = Параметры.Текст;
		СтрокаПозицияКурсора = НСтр("ru='ПозицияКурсора';uk='ПозицияКурсора'");
		СтрокаКурсора = ОпределитьНомерПозицииДляКурсора(Текст, СтрокаПозицияКурсора) - 9;
		Текст = СтрЗаменить(Текст, СтрокаПозицияКурсора, "");
		Содержание.УстановитьHTML(Текст, Новый Структура);
	КонецЕсли;
	
	Если Параметры.Свойство("Вложения") Тогда 
		Если ТипЗнч(Параметры.Вложения) = Тип("СписокЗначений") Тогда 
			Для каждого СтрокаСписка из Параметры.Вложения Цикл 
				Вложения.Добавить(СтрокаСписка.Значение, СтрокаСписка.Представление);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ОтображатьТему") Тогда 
		Элементы.Тема.Видимость = Параметры.ОтображатьТему;
	КонецЕсли;
	
	ИмяФайлаТехническихПараметров = ИнформационныйЦентрСервер.ПолучитьИмяФайлаТехническихПараметровДляСообщенияВтехПоддержку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МаксимальныйРазмерФайлов = ИнформационныйЦентрКлиент.МаксимальныйРазмерВложенийДляОтправкиСообщенияВПоддержкуСервиса();;
	
	УстановитьКурсорВШаблонеТекста();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПрикрепитьФайл(Элемент)
	
#Если ВебКлиент Тогда
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
#Иначе
	РасширениеПодключено = Истина;
#КонецЕсли
	
	ДобавитьВнешниеФайлы(РасширениеПодключено);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайл(Элемент)
	
	Для Итерация = 0 по ВыбираемыеФайлы.Количество() - 1 Цикл 
		
		Если ВыбираемыеФайлы.Получить(Итерация).ИмяКнопкиУдаления = Элемент.Имя Тогда 
			ИндексИмени = ПолучитьИндексЭлементаФормы(Элемент.Имя);
			УдалитьВсеПодчиненныеЭлементы(ИндексИмени);
			УдалитьИзВременногоХранилища(ВыбираемыеФайлы.Получить(Итерация).АдресХранилища);
			ЭлементСписка = Вложения.НайтиПоИдентификатору(ВыбираемыеФайлы.Получить(Итерация).ИдентификаторВСпискеЗначений);
			Если ЭлементСписка <> Неопределено Тогда 
				Вложения.Удалить(ЭлементСписка);
			КонецЕсли;
			ВыбираемыеФайлы.Получить(Итерация).Размер = 0;
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Отправить(Команда)
	
	Если Не ПроверитьЗаполнениеРеквизитов() Тогда 
		Возврат;
	КонецЕсли;
	
	РезультатОтправки = ОтправитьСообщениеСервер();
	Если РезультатОтправки Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru='Сообщение отправлено.';uk='Повідомлення відправлене.'"));
		Закрыть();
	Иначе
		ОчиститьСообщения();
		ПоказатьСообщениеПользователю(НСтр("ru='К сожалению сообщение не было отправлено."
"Повторите попытку позже.';uk='На жаль повідомлення не було відправлене."
"Повторіть спробу пізніше.'"));
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УдалитьВсеПодчиненныеЭлементы(ИндексЭлемента)
	
	НайденнаяЭлемент = Элементы.Найти("ГруппаФайла" + Строка(ИндексЭлемента));
	Если НайденнаяЭлемент <> Неопределено Тогда 
		Элементы.Удалить(НайденнаяЭлемент);
	КонецЕсли;
	
	НайденнаяЭлемент = Элементы.Найти("ТекстИмениФайла" + Строка(ИндексЭлемента));
	Если НайденнаяЭлемент <> Неопределено Тогда 
		Элементы.Удалить(НайденнаяЭлемент);
	КонецЕсли;
	
	НайденнаяЭлемент = Элементы.Найти("КнопкаУдаленияФайла" + Строка(ИндексЭлемента));
	Если НайденнаяЭлемент <> Неопределено Тогда 
		Элементы.Удалить(НайденнаяЭлемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИндексЭлементаФормы(ИмяЭлемента)
	
	НачалоПозиции = СтрДлина("КнопкаУдаленияФайла") + 1;
	Возврат Число(Сред(ИмяЭлемента, НачалоПозиции));
	
КонецФункции

&НаКлиенте
Функция ПроверитьЗаполнениеРеквизитов()
	
	Если ЗначениеЗаполнено(ОтКого) Тогда
		Попытка
			ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(ОтКого);
			Возврат Истина;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), ,
					"ОтКого");
			Возврат Ложь;
		КонецПопытки;
	Иначе 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Не заполнено поле Адрес ответа';uk='Не заповнене поле Адреса відповіді'"), ,
			"ОтКого");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьВнешниеФайлы(РасширениеПодключено)
	
	Если РасширениеПодключено Тогда 
		ПоместитьФайлыСРасширением();
	Иначе
		ПоместитьФайлыБезРасширения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьФайлыСРасширением()
	
	// Вызов диалога выбора файлов.
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = НСтр("ru='Выберите файл';uk='Виберіть файл'");
	Диалог.МножественныйВыбор = Истина;
	Если Не Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	ВыбранныеФайлы = Диалог.ВыбранныеФайлы;
	
	// Проверка на корректность общего размера файлов.
	Если Не ОбщийРазмерФайловОптималенКлиент(МаксимальныйРазмерФайлов, ВыбранныеФайлы) Тогда 
		ТекстПредупреждения = НСтр("ru='Размер выбранных файлов превышает предел в %1 Мб';uk='Розмір вибраних файлів перевищує межу у %1 Мб'");
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, МаксимальныйРазмерФайлов);
		ОчиститьСообщения();
		ПоказатьСообщениеПользователю(ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru='Подождите. Выбранные файлы добавляются к сообщению.';uk='Зачекайте. Вибрані файли додаються до повідомлення.'"));
	
	// Добавить файлы в таблицу.
	ДобавитьФайлыВТаблицуВыбираемыхФайлов(ВыбранныеФайлы);
	
	ДобавитьФайлыНаФорму();
	
	Состояние();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьФайлыНаФорму()
	
	// Создаются элементы для вложенных файлов.
	СоздатьЭлементыФормыДляВложенногоФайла();
	
	// Добавляются файлы в список значений.
	ДобавитьФайлыВСписокОтправляемых();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьФайлыБезРасширения()
	
	ПослеПомещенияФайловБезРасширения = Новый ОписаниеОповещения(
		"ПослеПомещенияФайловБезРасщирения", ЭтотОбъект);
	
	НачатьПомещениеФайла(
		ПослеПомещенияФайловБезРасширения,
		,
		,
		Истина,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПомещенияФайловБезРасщирения(Результат, АдресХранилища, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		
		ИмяФайла = ПолучитьИмяФайла(ВыбранноеИмяФайла);
		ПоместитьФайлыБезРасширенияНаСервере(АдресХранилища, ИмяФайла);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяФайла(ВыбранноеИмяФайла)
	
	ИмяФайла = ВыбранноеИмяФайла;
	
	Пока Найти(ИмяФайла, "/") <> 0 или
		Найти(ИмяФайла, "\") <> 0 Цикл
		
		ПозицияЭлемента = Найти(ВыбранноеИмяФайла, "/");
		ИмяФайла = Сред(ИмяФайла, ПозицияЭлемента + 1);
		
		ПозицияЭлемента = Найти(ВыбранноеИмяФайла, "\");
		ИмяФайла = Сред(ИмяФайла, ПозицияЭлемента + 1);
		
	КонецЦикла;
	
	Возврат ИмяФайла;
	
КонецФункции

&НаСервере
Процедура ПоместитьФайлыБезРасширенияНаСервере(АдресХранилища, ИмяФайла)
	
	НовыйФайл = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	// Проверка на корректность общего размера файлов.
	Если Не ОбщийРазмерФайловОптималенСервер(МаксимальныйРазмерФайлов, НовыйФайл) Тогда 
		ТекстПредупреждения = НСтр("ru='Размер выбранных файлов превышает предел в %1 Мб';uk='Розмір вибраних файлів перевищує межу у %1 Мб'");
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, МаксимальныйРазмерФайлов);
		ПоказатьСообщениеПользователю(ТекстПредупреждения);
		УдалитьИзВременногоХранилища(АдресХранилища);
		Возврат;
	КонецЕсли;
	
	// Удаление файла "TechnicalParameters.xml", если он имеется
	Если ВРег(ИмяФайла) = ВРег(ИмяФайлаТехническихПараметров) Тогда 
		ТекстПредупреждения = НСтр("ru=""Файл с именем '%1' нельзя прикладывать к сообщению."
"Переименуйте добавляемый файл."";uk=""Файл з іменем '%1' не можна прикладати до повідомлення."
"Перейменуйте новий файл.""");
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, ИмяФайла);
		ПоказатьСообщениеПользователю(ТекстПредупреждения);
		УдалитьИзВременногоХранилища(АдресХранилища);
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы				 = ВыбираемыеФайлы.Добавить();
	СтрокаТаблицы.ИмяФайла		 = ИмяФайла;
	СтрокаТаблицы.Размер		 = НовыйФайл.Размер() / 1024;
	СтрокаТаблицы.АдресХранилища = АдресХранилища;
	
	СоздатьЭлементыФормыДляВложенногоФайла();
	
	ДобавитьФайлыВСписокОтправляемых();
	
КонецПроцедуры

&НаКлиенте
Функция ОбщийРазмерФайловОптималенКлиент(МаксимальныйРазмер, ВыбранныеФайлы)
	
	Если ПолучитьОбщийРазмерВыбранногоФайла(ВыбранныеФайлы) > МаксимальныйРазмер Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ОбщийРазмерФайловОптималенСервер(МаксимальныйРазмерФайлов, НовыйФайл)
	
	Размер = НовыйФайл.Размер() / 1024 / 1024 ;
	
	// Подсчет общего размера приложенных к письму файлов (с установленной пометкой).
	Для Итерация = 0 по ВыбираемыеФайлы.Количество() - 1 Цикл
		Размер	= Размер + (ВыбираемыеФайлы.Получить(Итерация).Размер / 1024);
	КонецЦикла;
	
	Если Размер > МаксимальныйРазмерФайлов Тогда 
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПолучитьОбщийРазмерВыбранногоФайла(ВыбранныеФайлы)
	
	Размер = 0;
	
	Для Итерация = 0 по ВыбранныеФайлы.Количество() -1 Цикл 
		ТекущийФайл		= Новый Файл(ВыбранныеФайлы.Получить(Итерация));
		Размер			= Размер + ТекущийФайл.Размер();
	КонецЦикла;
	
	// Подсчет общего размера приложенных к письму файлов (с установленной пометкой).
	Для Итерация = 0 по ВыбираемыеФайлы.Количество() - 1 Цикл
		Размер	= Размер + (ВыбираемыеФайлы.Получить(Итерация).Размер / 1024);
	КонецЦикла;
	
	РазмерВМегабайтах = Размер / 1024 / 1024;
	
	Возврат РазмерВМегабайтах;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьФайлыВТаблицуВыбираемыхФайлов(ВыбранныеФайлы)
	
#Если ВебКлиент Тогда
	МассивОпераций = Новый Массив;
	Для Итерация = 0 По ВыбранныеФайлы.Количество() - 1 Цикл
		ОписаниеВызова = Новый Массив;
		ОписаниеВызова.Добавить("ПоместитьФайлы");
		
		ПомещаемыеФайлы = Новый Массив;
		Описание = Новый ОписаниеПередаваемогоФайла(ВыбранныеФайлы.Получить(Итерация));
		ПомещаемыеФайлы.Добавить(Описание);
		ОписаниеВызова.Добавить(ПомещаемыеФайлы);
		
		ОписаниеВызова.Добавить(Неопределено);
		ОписаниеВызова.Добавить(Неопределено);
		ОписаниеВызова.Добавить(Ложь);
		
		МассивОпераций.Добавить(ОписаниеВызова);
	КонецЦикла;
	
	Если Не ЗапроситьРазрешениеПользователя(МассивОпераций) Тогда 
		Возврат;
	КонецЕсли;
#КонецЕсли

	Для Итерация = 0 По ВыбранныеФайлы.Количество() - 1 Цикл 
		
		ВыбранныйФайл = Новый Файл(ВыбранныеФайлы.Получить(Итерация));
		// Удаление файла "TechnicalParameters.xml", если он имеется
		Если ВРег(ВыбранныйФайл.Имя) = ВРег(ИмяФайлаТехническихПараметров) Тогда 
			ТекстПредупреждения = НСтр("ru=""Файл с именем '%1' нельзя прикладывать к сообщению."
"Переименуйте добавляемый файл."";uk=""Файл з іменем '%1' не можна прикладати до повідомлення."
"Перейменуйте новий файл.""");
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, ВыбранныйФайл.Имя);
			ОчиститьСообщения();
			ПоказатьСообщениеПользователю(ТекстПредупреждения);
			Продолжить;
		КонецЕсли;
		
		// Заполнение таблицы значений.
		СтрокаТаблицы				= ВыбираемыеФайлы.Добавить();
		СтрокаТаблицы.ИмяФайла		= ВыбранныйФайл.Имя;
		СтрокаТаблицы.Размер		= ВыбранныйФайл.Размер() / 1024;
		
		ПомещаемыеФайлы = Новый Массив;
		Описание = Новый ОписаниеПередаваемогоФайла(ВыбранныйФайл.ПолноеИмя);
		ПомещаемыеФайлы.Добавить(Описание);
		ПомещенныеФайлы = Новый Массив;
		
		Попытка
			Реузльтат = ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы, , Ложь, УникальныйИдентификатор);
		Исключение
			Реузльтат = Ложь;
		КонецПопытки;
		
		Если Не Реузльтат Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Ошибка при загрузке файла %1';uk='Помилка при завантаженні файлу %1'"), ВыбранныйФайл.Имя);
			ОчиститьСообщения();
			ПоказатьСообщениеПользователю(СообщениеОбОшибке);
			ВыбираемыеФайлы.Удалить(ВыбираемыеФайлы.Индекс(СтрокаТаблицы));
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТаблицы.АдресХранилища = ПомещенныеФайлы.Получить(0).Хранение;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьФайлыВСписокОтправляемых()
	
	Для каждого ТекущийФайл из ВыбираемыеФайлы Цикл 
		
		Если ТекущийФайл.ИдентификаторВСпискеЗначений <> 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		ИмяФайла = ТекущийФайл.ИмяФайла;
		АдресХранилища = ТекущийФайл.АдресХранилища;
		НовыйФайлВложения = Вложения.Добавить(ПолучитьИзВременногоХранилища(АдресХранилища), ИмяФайла);
		ТекущийФайл.ИдентификаторВСпискеЗначений = НовыйФайлВложения.ПолучитьИдентификатор();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОтправитьСообщениеСервер()
	
	Текст = "";
	ВложенияHTML = Неопределено;
	Содержание.ПолучитьHTML(Текст, ВложенияHTML);
	
	ПараметрыСообщения = Новый Структура();
	ПараметрыСообщения.Вставить("ОтКого",    ОтКого);
	ПараметрыСообщения.Вставить("Тема",      ОпределитьТему());
	ПараметрыСообщения.Вставить("Текст",     Текст);
	ПараметрыСообщения.Вставить("Вложения",  Вложения);
	ПараметрыСообщения.Вставить("ТипТекста", "HTML");
	
	Результат = Истина;
	ИнформационныйЦентрСервер.ПриОтправкеСообщенияПользователяВТехподдержку(ПараметрыСообщения, Результат);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ОпределитьТему()
	
	Если Не ПустаяСтрока(Тема) Тогда 
		Возврат Тема;
	КонецЕсли;
	
	ТекстСообщения = Содержание.ПолучитьТекст();
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "Здравствуйте!", "");
	
	Возврат СокрЛП(ТекстСообщения);
	
КонецФункции

&НаКлиенте
Процедура УстановитьКурсорВШаблонеТекста()
	
	ПодключитьОбработчикОжидания("ОбработчикУстановитьКурсорВШаблонеТекста", 0.5, Истина);
	
КонецПроцедуры

&НаСервере
Функция ОпределитьНомерПозицииДляКурсора(ТекстПараметр, СтрокаПозицияКурсора)
	
	Возврат Найти(ТекстПараметр, СтрокаПозицияКурсора);
	
КонецФункции

&НаКлиенте
Процедура ОбработчикУстановитьКурсорВШаблонеТекста()
	
	ТекущийЭлемент = Элементы.Содержание;
	Закладка = Содержание.ПолучитьЗакладкуПоПозиции(СтрокаКурсора);
	Элементы.Содержание.УстановитьГраницыВыделения(Закладка, Закладка);
	
КонецПроцедуры

&НаСервере
Функция СоздатьЭлементыФормыДляВложенногоФайла()
	
	Для Итерация = 0 по ВыбираемыеФайлы.Количество() - 1 Цикл
		
		Если Не ПустаяСтрока(ВыбираемыеФайлы.Получить(Итерация).ИмяКнопкиУдаления) Тогда 
			Продолжить;
		КонецЕсли;
		
		ГруппаФайла							= Элементы.Добавить("ГруппаФайла" + Строка(Итерация), Тип("ГруппаФормы"), Элементы.ГруппаПрикрепленныхФайлов);
		ГруппаФайла.Вид						= ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаФайла.ОтображатьЗаголовок		= Ложь;
		ГруппаФайла.Группировка				= ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ГруппаФайла.Отображение				= ОтображениеОбычнойГруппы.Нет;
		
		ТекстИмениФайла						= Элементы.Добавить("ТекстИмениФайла" + Строка(Итерация), Тип("ДекорацияФормы"), ГруппаФайла);
		ТекстИмениФайла.Вид					= ВидДекорацииФормы.Надпись;
		ТекстИмениФайла.Заголовок			= ВыбираемыеФайлы.Получить(Итерация).ИмяФайла + " (" + ВыбираемыеФайлы.Получить(Итерация).Размер + " Кб)";
		
		КнопкаУдаленияФайла					= Элементы.Добавить("КнопкаУдаленияФайла" + Строка(Итерация), Тип("ДекорацияФормы"), ГруппаФайла);
		КнопкаУдаленияФайла.Вид				= ВидДекорацииФормы.Картинка;
		КнопкаУдаленияФайла.Картинка		= БиблиотекаКартинок.УдалитьНепосредственно;
		КнопкаУдаленияФайла.Подсказка		= НСтр("ru='Удалить файл';uk='Вилучити файл'");
		КнопкаУдаленияФайла.Ширина			= 2;
		КнопкаУдаленияФайла.Высота			= 1;
		КнопкаУдаленияФайла.РазмерКартинки	= РазмерКартинки.Растянуть;
		КнопкаУдаленияФайла.Гиперссылка		= Истина;
		КнопкаУдаленияФайла.УстановитьДействие("Нажатие", "УдалитьФайл");
		
		ВыбираемыеФайлы.Получить(Итерация).ИмяКнопкиУдаления = КнопкаУдаленияФайла.Имя;
		
	КонецЦикла;
	
	// Подключается обработчик ожидания добавления файла.
	ДобавитьФайлыВСписокОтправляемых();
	
КонецФункции

&НаСервере
Функция ПоказатьСообщениеПользователю(Текст)
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
	
КонецФункции