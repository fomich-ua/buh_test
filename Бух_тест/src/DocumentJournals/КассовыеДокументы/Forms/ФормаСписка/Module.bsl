////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначения.ПриСозданииФормыНаСервере(ЭтаФорма, СтандартнаяОбработка, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	ОтборОрганизация = ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтаФорма);
	
	Элементы.ОтборКонтрагент.ОграничениеТипа = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиФормыЖурналаКассовыеДокументы", "ТипОтбораКонтрагент", 
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	
	Если Параметры.Отбор.Свойство("Контрагент") Тогда
		ОтборКонтрагент = Параметры.Отбор.Контрагент;
		Если ТипЗнч(ОтборКонтрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица");
		КонецЕсли;
		Если ТипЗнч(ОтборКонтрагент) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
			Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчета");
		КонецЕсли;
		Если ТипЗнч(ОтборКонтрагент) = Тип("СправочникСсылка.Склады") Тогда
			Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Склады");
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица") Тогда
		Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораФизическоеЛицо;
	ИначеЕсли Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчета") Тогда
		Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораБанковскийСчет;
	ИначеЕсли Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Склады") Тогда
		Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораСклад;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОтборОрганизация = ОбщегоНазначенияБПКлиент.ИзменитьОтборПоОсновнойОрганизации(Список, ,Параметр);
	КонецЕсли;	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Организация", ОтборОрганизация, ЗначениеЗаполнено(ОтборОрганизация));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Список, 
		"Контрагент", 
		ОтборКонтрагент, 
		ЗначениеЗаполнено(ОтборКонтрагент));
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СоздатьПриходныйКассовыйОрдер(Команда)
	
	СтруктураОтбора = ОбщегоНазначенияБПКлиентСервер.ПолучитьСтруктуруОтбораСписка(ЭтаФорма.Список.Отбор.Элементы);
	
	ОткрытьФорму("Документ.ПриходныйКассовыйОрдер.ФормаОбъекта",
		Новый Структура("ЗначенияЗаполнения", СтруктураОтбора), 
		ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРасходныйКассовыйОрдер(Команда)
	
	СтруктураОтбора = ОбщегоНазначенияБПКлиентСервер.ПолучитьСтруктуруОтбораСписка(ЭтаФорма.Список.Отбор.Элементы);
	
	ОткрытьФорму("Документ.РасходныйКассовыйОрдер.ФормаОбъекта",
		Новый Структура("ЗначенияЗаполнения", СтруктураОтбора), 
		ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКассовуюКнигу(Команда)
	
	СтруктураПараметров = Новый Структура("Организация", ОтборОрганизация);
	ОткрытьФорму("Отчет.КассоваяКнига.Форма", СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТипОтбораКонтрагент(Команда)
	
	Если Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораКонтрагент Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораКонтрагент;
	
	Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("НастройкиФормыЖурналаКассовыеДокументы", "ТипОтбораКонтрагент", 
		Элементы.ОтборКонтрагент.ОграничениеТипа);
	
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда
	
		ОтборКонтрагент = Неопределено;
		
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
			Список, 
			"Контрагент", 
			ОтборКонтрагент, 
			ЗначениеЗаполнено(ОтборКонтрагент));
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТипОтбораФизическоеЛицо(Команда)
	
	Если Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораФизическоеЛицо Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораФизическоеЛицо;
	
	Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица");
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("НастройкиФормыЖурналаКассовыеДокументы", "ТипОтбораКонтрагент", 
		Элементы.ОтборКонтрагент.ОграничениеТипа);
	
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда
	
		ОтборКонтрагент = Неопределено;
		
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
			Список, 
			"Контрагент", 
			ОтборКонтрагент, 
			ЗначениеЗаполнено(ОтборКонтрагент));
			
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТипОтбораБанковскийСчет(Команда)
	
	Если Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораБанковскийСчет Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораБанковскийСчет;
	
	Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчета");
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("НастройкиФормыЖурналаКассовыеДокументы", "ТипОтбораКонтрагент", 
		Элементы.ОтборКонтрагент.ОграничениеТипа);
	
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда
	
		ОтборКонтрагент = Неопределено;
		
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
			Список, 
			"Контрагент", 
			ОтборКонтрагент, 
			ЗначениеЗаполнено(ОтборКонтрагент));
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТипОтбораСклад(Команда)
	
	Если Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораСклад Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаПанельВыбораТипаОтбора.ТекущаяСтраница = Элементы.ГруппаТипОтбораСклад;
	
	Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Склады");
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("НастройкиФормыЖурналаКассовыеДокументы", "ТипОтбораКонтрагент", 
		Элементы.ОтборКонтрагент.ОграничениеТипа);
	
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда
	
		ОтборКонтрагент = Неопределено;
		
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
			Список, 
			"Контрагент", 
			ОтборКонтрагент, 
			ЗначениеЗаполнено(ОтборКонтрагент));
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВернутьПараметрыОткрытия()
	Возврат Новый Структура("Ключ, Заголовок, Период, Организация", Список, НСтр("ru='Реестр кассовых документов ';uk='Реєстр касових документів '"), Элементы.Список.Период, ОтборОрганизация);
КонецФункции

&НаКлиенте
Процедура ПечатьРеестра(Команда)
	ФормаПечатиРеестра = ПолучитьФорму("Отчет.РеестрДокументов.Форма.ФормаОтчета", ВернутьПараметрыОткрытия(), ЭтаФорма, Новый УникальныйИдентификатор);
	ФормаПечатиРеестра.Открыть();
КонецПроцедуры