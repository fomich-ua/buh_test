#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает XDTO-тип, описывающий разрешения типа, соответствующего элементу кэша.
//
// Возвращаемое значение - ТипОбъектаXDTO.
//
Функция ТипXDTOПредставленияРазрешений() Экспорт
	
	Возврат ФабрикаXDTO.Тип(РаботаВБезопасномРежимеСлужебный.ПакетXDTOПредставленийРазрешений(), "InternetResourceAccess");
	
КонецФункции

// Формирует набор записей текущего регистра кэша из XDTO-представлений разрешения.
//
// Параметры:
//  БезопасныйРежим - ОпределяемыйТип.БезопасныйРежим,
//  Владелец - ЛюбаяСсылка,
//  XDTOПредставления - Массив(ОбъектXDTO).
//
// Возвращаемое значение - РегистрСведенийНаборЗаписей.
//
Функция НаборЗаписейИзXDTOПредставления(Знач XDTOПредставления, Знач БезопасныйРежим, Знач Владелец, Знач ДляУдаления) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.БезопасныйРежим.Установить(БезопасныйРежим);
	Набор.Отбор.Владелец.Установить(Владелец);
	
	Если ДляУдаления Тогда
		
		Возврат Набор;
		
	Иначе
		
		Таблица = Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсовВМоделиСервиса.ТаблицаРазрешений(СоздатьНаборЗаписей().Метаданные(), Истина);
		
		Для Каждого XDTOПредставление Из XDTOПредставления Цикл
			
			Псевдоним = НРег(XDTOПредставление.Protocol) + ":\\" + НРег(XDTOПредставление.Host) + ":" + Формат(XDTOПредставление.Port, "ЧГ=0");
			
			Ключ = Новый Структура("БезопасныйРежим,Владелец,Псевдоним", БезопасныйРежим, Владелец, Псевдоним);
			Если Таблица.НайтиСтроки(Ключ).Количество() = 0 Тогда
				
				Строка = Таблица.Добавить();
				Строка.БезопасныйРежим = БезопасныйРежим;
				Строка.Владелец = Владелец;
				Строка.Псевдоним = Псевдоним;
				Строка.Протокол = НРег(XDTOПредставление.Protocol);
				Строка.Адрес = НРег(XDTOПредставление.Host);
				Строка.Порт = НРег(XDTOПредставление.Port);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Набор.Загрузить(Таблица);
		Возврат Набор;
		
	КонецЕсли;
	
КонецФункции

// Возвращает текст запроса для получения текущего среза разрешений по данному
//  элементу кэша.
//
// Возвращаемое значение - Строка, текст запроса.
//
Функция ЗапросТекущегоСреза() Экспорт
	
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РазрешенныеИнтернетРесурсыОбластейДанных.Псевдоним КАК Псевдоним,
		|	РазрешенныеИнтернетРесурсыОбластейДанных.Протокол КАК Протокол,
		|	РазрешенныеИнтернетРесурсыОбластейДанных.Адрес КАК Адрес,
		|	РазрешенныеИнтернетРесурсыОбластейДанных.Порт КАК Порт,
		|	РазрешенныеИнтернетРесурсыОбластейДанных.ВнешнийМодуль КАК ВнешнийМодуль
		|ИЗ
		|	РегистрСведений.РазрешенныеИнтернетРесурсыОбластейДанных КАК РазрешенныеИнтернетРесурсыОбластейДанных";
	
КонецФункции

// Возвращает текст запроса для получения дельты измения разрешений по данному
//  элементу кэша.
//
// Возвращаемое значение - Строка, текст запроса.
//
Функция ЗапросПолученияДельты() Экспорт
	
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_До.Псевдоним,
		|	ВТ_До.Протокол,
		|	ВТ_До.Адрес,
		|	ВТ_До.Порт,
		|	ВТ_До.ВнешнийМодуль
		|ИЗ
		|	ВТ_До КАК ВТ_До ЛЕВОЕ СОЕДИНЕНИЕ ВТ_После КАК ВТ_После
		|		ПО ВТ_До.ВнешнийМодуль = ВТ_После.ВнешнийМодуль
		|			И ВТ_До.Псевдоним = ВТ_После.Псевдоним
		|ГДЕ
		|	ВТ_После.Псевдоним ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_После.Псевдоним,
		|	ВТ_После.Протокол,
		|	ВТ_После.Адрес,
		|	ВТ_После.Порт,
		|	ВТ_После.ВнешнийМодуль
		|ИЗ
		|	ВТ_После КАК ВТ_После ЛЕВОЕ СОЕДИНЕНИЕ ВТ_До КАК ВТ_До
		|		ПО ВТ_После.ВнешнийМодуль = ВТ_До.ВнешнийМодуль
		|			И ВТ_После.Псевдоним = ВТ_До.Псевдоним
		|ГДЕ
		|	ВТ_До.Псевдоним ЕСТЬ NULL";
	
КонецФункции

// Проверяет разрешение на допустимость.
//
// Параметры:
//  Разрешение - ОбъектXDTO.
//
Процедура ПроверитьРазрешение(Разрешение) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли