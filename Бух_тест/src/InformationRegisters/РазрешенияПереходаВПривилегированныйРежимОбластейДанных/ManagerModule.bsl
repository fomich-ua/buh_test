#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает XDTO-тип, описывающий разрешения типа, соответствующего элементу кэша.
//
// Возвращаемое значение - ТипОбъектаXDTO.
//
Функция ТипXDTOПредставленияРазрешений() Экспорт
	
	Возврат ФабрикаXDTO.Тип(РаботаВБезопасномРежимеСлужебный.ПакетXDTOПредставленийРазрешений(), "ExternalModulePrivilegedModeAllowed");
	
КонецФункции

// Формирует набор записей текущего регистра кэша из XDTO-представлений разрешения.
//
// Параметры:
//  БезопасныйРежим - ОпределяемыйТип.БезопасныйРежим,
//  Владелец - ЛюбаяСсылка,
//  XDTOПредставления - Массив(ОбъектXDTO).
//
// Возвращаемое значение - РегистрСведенийНаборЗаписей.
//
Функция НаборЗаписейИзXDTOПредставления(Знач XDTOПредставления, Знач БезопасныйРежим, Знач Владелец, Знач ДляУдаления) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.БезопасныйРежим.Установить(БезопасныйРежим);
	Набор.Отбор.Владелец.Установить(Владелец);
	
	Если ДляУдаления Тогда
		
		Возврат Набор;
		
	Иначе
		
		Таблица = Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсовВМоделиСервиса.ТаблицаРазрешений(СоздатьНаборЗаписей().Метаданные(), Истина);
		
		Для Каждого XDTOПредставление Из XDTOПредставления Цикл
			
			Ключ = Новый Структура("БезопасныйРежим,Владелец", БезопасныйРежим, Владелец);
			Если Таблица.НайтиСтроки(Ключ).Количество() = 0 Тогда
				Строка = Таблица.Добавить();
				Строка.БезопасныйРежим = БезопасныйРежим;
				Строка.Владелец = Владелец;
			КонецЕсли;
			
		КонецЦикла;
		
		Набор.Загрузить(Таблица);
		Возврат Набор;
		
	КонецЕсли;
	
КонецФункции

// Возвращает текст запроса для получения текущего среза разрешений по данному
//  элементу кэша.
//
// Возвращаемое значение - Строка, текст запроса.
//
Функция ЗапросТекущегоСреза() Экспорт
	
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИСТИНА КАК РазрешенПереходВПривилегированныйРежим,
		|	РазрешенияПереходаВПривилегированныйРежимОбластейДанных.ВнешнийМодуль КАК ВнешнийМодуль
		|ИЗ
		|	РегистрСведений.РазрешенияПереходаВПривилегированныйРежимОбластейДанных КАК РазрешенияПереходаВПривилегированныйРежимОбластейДанных";
	
КонецФункции

// Возвращает текст запроса для получения дельты измения разрешений по данному
//  элементу кэша.
//
// Возвращаемое значение - Строка, текст запроса.
//
Функция ЗапросПолученияДельты() Экспорт
	
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИСТИНА КАК РазрешенПереходВПривилегированныйРежим,
		|	ВТ_До.ВнешнийМодуль КАК ВнешнийМодуль
		|ИЗ
		|	ВТ_До КАК ВТ_До
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_После КАК ВТ_После
		|		ПО ВТ_До.БезопасныйРежим = ВТ_После.БезопасныйРежим
		|ГДЕ
		|	ВТ_После.БезопасныйРежим ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИСТИНА КАК РазрешенПереходВПривилегированныйРежим,
		|	ВТ_После.ВнешнийМодуль КАК ВнешнийМодуль
		|ИЗ
		|	ВТ_После КАК ВТ_После
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_До КАК ВТ_До
		|		ПО ВТ_После.ВнешнийМодуль = ВТ_До.ВнешнийМодуль
		|ГДЕ
		|	ВТ_До.БезопасныйРежим ЕСТЬ NULL";
	
КонецФункции

// Проверяет разрешение на допустимость.
//
// Параметры:
//  Разрешение - ОбъектXDTO.
//
Процедура ПроверитьРазрешение(Разрешение) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли