
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	МестоЗапуска = Параметры.МестоЗапуска;
	
	ОписаниеОшибки = Параметры.ОписаниеОшибки;
	ОписаниеОшибкиЗаполнено = НЕ ПустаяСтрока(ОписаниеОшибки);
	
	КлючСохраненияПоложенияОкна = МестоЗапуска + Строка(ОписаниеОшибкиЗаполнено);
	
	Если ТипЗнч(Параметры.СтартовыеПараметры) = Тип("Структура") Тогда
		СтартовыеПараметры = Параметры.СтартовыеПараметры;
	Иначе
		СтартовыеПараметры = Новый Структура;
	КонецЕсли;
	
	Элементы.ОписаниеОшибки.Видимость = ОписаниеОшибкиЗаполнено;
	
	Элементы.ДекорацияПараметры.Видимость =
		ИнтернетПоддержкаПользователейВызовСервера.ДоступнаНастройкаПараметровПодключенияКИнтернетПоддержке();
	
	Если МестоЗапуска <> "systemStartNew" Тогда
		
		Элементы.ЗапускатьПриСтарте.Видимость = Ложь;
		
	Иначе
		
		ЗапускМеханизмаПриСтарте = Ложь;
		ИнтернетПоддержкаПользователейСерверПереопределяемый.ЗапускатьИнтернетПоддержкуПриСтартеПрограммы(
			ЗапускМеханизмаПриСтарте);
		
		Если ЗапускМеханизмаПриСтарте = Истина Тогда
			// Если форма ошибки отобразилась при МестоЗапуска = "systemStartNew",
			// тогда настройка ЗапускатьПриСтарте установлена в Истина
			ЗапускатьПриСтарте = Истина;
		Иначе
			Элементы.ЗапускатьПриСтарте.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	// Настройка внешнего вида формы
	Если ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
		Элементы.ГруппаЗаголовка.Отображение           = ОтображениеОбычнойГруппы.СлабоеВыделение;
		Элементы.ГруппаИнформационнойЧасти.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Элементы.ДекорацияПараметры.Видимость Тогда
		Элементы.ДекорацияПараметры.Видимость = НЕ ФормаПараметровИнтернетПоддержкиОткрыта();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияПараметрыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "WebITSParams" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьФормуНастроекПараметровПодключения(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСообщенияВТехПоддержкуОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "SendMail" Тогда
		СтандартнаяОбработка = Ложь;
		Попытка
			ЗапуститьПриложение("mailto:webits-info@1c.ua");
		Исключение
			ИнтернетПоддержкаПользователейВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				НСтр("ru='Не удалось открыть почтовый клиент: ';uk='Не вдалося відкрити поштовий клієнт: '")
					+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ПоказатьПредупреждение(,
				НСтр("ru='Не удалось запустить приложение для работы с электронной почтой.';uk='Не вдалося запустити додаток для роботи з електронною поштою.'"));
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускатьПриСтартеПриИзменении(Элемент)
	
	УстановитьНастройкуЗапускатьПриСтарте(ЗапускатьПриСтарте);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПовторитьПопыткуПодключения(Команда)
	
	Закрыть();
	
	Если МестоЗапуска = "systemStartNew" Тогда
		МестоЗапуска = "handStartNew";
	КонецЕсли;
	
	ИнтернетПоддержкаПользователейКлиент.СтартоватьМеханизм(МестоЗапуска, СтартовыеПараметры, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ФормаПараметровИнтернетПоддержкиОткрыта()
	
	ПараметрыОповещения = Новый Структура("ФормаОткрыта", Ложь);
	Оповестить("ПроверитьОткрытиеФормыПараметровИнтернетПоддержки", ПараметрыОповещения);
	Возврат ПараметрыОповещения.ФормаОткрыта;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьНастройкуЗапускатьПриСтарте(ЗначениеНастройки)
	
	ХранилищеОбщихНастроек.Сохранить(
		"ИнтернетПоддержкаПользователей",
		"ВсегдаПоказыватьПриСтартеПрограммы",
		ЗначениеНастройки);
	
КонецПроцедуры

#КонецОбласти
