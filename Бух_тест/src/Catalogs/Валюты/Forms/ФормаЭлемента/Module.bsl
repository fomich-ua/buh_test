////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Если Параметры.Свойство("КодВалюты") Тогда
			Объект.Код = Параметры.КодВалюты;
		КонецЕсли;
		
		Если Параметры.Свойство("НаименованиеКраткое") Тогда
			Объект.Наименование = Параметры.НаименованиеКраткое;
		КонецЕсли;
		
		Если Параметры.Свойство("НаименованиеПолное") Тогда
			Объект.НаименованиеПолное = Параметры.НаименованиеПолное;
		КонецЕсли;
		
		Если Параметры.Свойство("Загружается") Тогда
			Объект.ЗагружаетсяИзИнтернета = Параметры.Загружается;
		КонецЕсли;
		
		Если Объект.ЗагружаетсяИзИнтернета Тогда
			Объект.ОсновнаяВалюта = Справочники.Валюты.ПустаяСсылка();
		КонецЕсли;
		
		Если Параметры.Свойство("ПараметрыПрописиНаРусском") Тогда
			Объект.ПараметрыПрописиНаРусском = Параметры.ПараметрыПрописиНаРусском;
		КонецЕсли;
		
		Если Параметры.Свойство("ПараметрыПрописиНаУкраинском") Тогда
			Объект.ПараметрыПрописиНаУкраинском = Параметры.ПараметрыПрописиНаУкраинском;
		КонецЕсли;
		
		ЗаполнитьФормуПоОбъекту();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьФормуПоОбъекту();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗависимыйКурсВалют Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ОсновнаяВалюта) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru='Необходимо указать основную валюту';uk='Необхідно вказати основну валюту'"), ,
				"Объект.ОсновнаяВалюта", ,
				Отказ);
		КонецЕсли;
	Иначе
		Объект.ОсновнаяВалюта = Неопределено;
		Объект.Наценка = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ПараметрыПрописиНаРусском = ПараметрыПрописиНаРусском(ЭтаФорма);
	ТекущийОбъект.ПараметрыПрописиНаУкраинском = ПараметрыПрописиНаУкраинском(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// Страница "Основные сведения"

&НаКлиенте
Процедура ЗагружаетсяИзИнтернетаПриИзменении(Элемент)
	
	Если Объект.ЗагружаетсяИзИнтернета Тогда
		ЗависимыйКурсВалют = Ложь;
		Объект.ОсновнаяВалюта = Неопределено;
		Объект.Наценка = 0;
	КонецЕсли;
	
	УстановитьСвойстваЭлементовГруппыЗависимойВалюты(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗависимыйКурсВалютПриИзменении(Элемент)
	
	УстановитьСвойстваЭлементовГруппыЗависимойВалюты(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяВалютаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПодготовитьДанныеВыбораПодчиненнойВалюты(ДанныеВыбора, Объект.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Страница "Параметры прописи валюты"

&НаКлиенте
Процедура СуммаЧислоПриИзменении(Элемент)
	
	УстановитьСуммуПрописью(ЭтаФорма);
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи4наРусскомПриИзменении(Элемент)
	УстановитьСклоненияПараметровПрописи(ЭтаФорма);
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи4наРусскомАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи4наРусскомОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи8наРусскомПриИзменении(Элемент)
	УстановитьСклоненияПараметровПрописи(ЭтаФорма);
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи8наРусскомАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи8наРусскомОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи1наРусскомПриИзменении(Элемент)
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи2наРусскомПриИзменении(Элемент)
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи3наРусскомПриИзменении(Элемент)
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи5наРусскомПриИзменении(Элемент)
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи6наРусскомПриИзменении(Элемент)
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи7наРусскомПриИзменении(Элемент)
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДлинаДробнойЧастиПриИзменении(Элемент)
	УстановитьСуммуПрописью(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДлинаДробнойЧастиАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаДробнойЧастиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

// Украинский

&НаКлиенте
Процедура СуммаЧислоУкрПриИзменении(Элемент)
	
	УстановитьСуммуПрописью(ЭтаФорма);
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи4наУкраинскомПриИзменении(Элемент)
	УстановитьСклоненияПараметровПрописиУкр(ЭтаФорма);
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи4наУкраинскомАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи4наУкраинскомОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи8наУкраинскомПриИзменении(Элемент)
	УстановитьСклоненияПараметровПрописиУкр(ЭтаФорма);
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи8наУкраинскомАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи8наУкраинскомОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи1наУкраинскомПриИзменении(Элемент)
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи2наУкраинскомПриИзменении(Элемент)
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи3наУкраинскомПриИзменении(Элемент)
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи5наУкраинскомПриИзменении(Элемент)
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи6наУкраинскомПриИзменении(Элемент)
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи7наУкраинскомПриИзменении(Элемент)
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДлинаДробнойЧастиУкрПриИзменении(Элемент)
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДлинаДробнойЧастиУкрАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаДробнойЧастиУкрОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьФормуПоОбъекту()
	
	ЗависимыйКурсВалют = ЗначениеЗаполнено(Объект.ОсновнаяВалюта);
	
	ПрочитатьПараметрыПрописи();
	
	УстановитьСвойстваЭлементовГруппыЗависимойВалюты(ЭтаФорма);
	УстановитьСклоненияПараметровПрописи(ЭтаФорма);
	УстановитьСклоненияПараметровПрописиУкр(ЭтаФорма);
	УстановитьСуммуПрописью(ЭтаФорма);
	УстановитьСуммуПрописьюУкр(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПрописиНаРусском(Форма)
	
	Возврат Форма.ПолеПрописи1наРусском + ", "
			+ Форма.ПолеПрописи2наРусском + ", "
			+ Форма.ПолеПрописи3наРусском + ", "
			+ НРег(Лев(Форма.ПолеПрописи4наРусском, 1)) + ", "
			+ Форма.ПолеПрописи5наРусском + ", "
			+ Форма.ПолеПрописи6наРусском + ", "
			+ Форма.ПолеПрописи7наРусском + ", "
			+ НРег(Лев(Форма.ПолеПрописи8наРусском, 1)) + ", "
			+ Форма.ДлинаДробнойЧасти;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПрописиНаУкраинском(Форма)
	
	Возврат Форма.ПолеПрописи1наУкраинском + ", "
			+ Форма.ПолеПрописи2наУкраинском + ", "
			+ Форма.ПолеПрописи3наУкраинском + ", "
			+ НРег(Лев(Форма.ПолеПрописи4наУкраинском, 1)) + ", "
			+ Форма.ПолеПрописи5наУкраинском + ", "
			+ Форма.ПолеПрописи6наУкраинском + ", "
			+ Форма.ПолеПрописи7наУкраинском + ", "
			+ НРег(Лев(Форма.ПолеПрописи8наУкраинском, 1)) + ", "
			+ Форма.ДлинаДробнойЧастиУкр;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСуммуПрописью(Форма)
	
	Форма.СуммаПрописью = ЧислоПрописью(Форма.СуммаЧисло, , ПараметрыПрописиНаРусском(Форма));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСуммуПрописьюУкр(Форма)
	
	Форма.СуммаПрописьюУкр = ЧислоПрописью(Форма.СуммаЧисло, "Л=uk_UA", ПараметрыПрописиНаУкраинском(Форма));
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПараметрыПрописи()
	
	// Считывает параметры прописи и заполняет соответствующие поля диалога.
	
	// Русский
	СтрокаПараметров = СтрЗаменить(Объект.ПараметрыПрописиНаРусском, ",", Символы.ПС);
	
	ПолеПрописи1наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 1));
	ПолеПрописи2наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 2));
	ПолеПрописи3наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 3));
	
	Род = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 4));
	
	Если	  НРег(Род) = "м" Тогда
		ПолеПрописи4наРусском = "Мужской";
	ИначеЕсли НРег(Род) = "ж" Тогда
		ПолеПрописи4наРусском = "Женский";
	ИначеЕсли НРег(Род) = "с" Тогда
		ПолеПрописи4наРусском = "Средний";
	КонецЕсли;
	
	ПолеПрописи5наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 5));
	ПолеПрописи6наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 6));
	ПолеПрописи7наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 7));
	
	Род = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 8));
	
	Если	  НРег(Род = "м") Тогда
		ПолеПрописи8наРусском = "Мужской";
	ИначеЕсли НРег(Род = "ж") Тогда
		ПолеПрописи8наРусском = "Женский";
	ИначеЕсли НРег(Род = "с") Тогда
		ПолеПрописи8наРусском = "Средний";
	КонецЕсли;
	
	ДлинаДробнойЧасти     = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 9));
	
	// Украинский
	СтрокаПараметров = СтрЗаменить(Объект.ПараметрыПрописиНаУкраинском, ",", Символы.ПС);
	
	ПолеПрописи1наУкраинском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 1));
	ПолеПрописи2наУкраинском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 2));
	ПолеПрописи3наУкраинском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 3));
	
	Род = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 4));
	
	Если	  НРег(Род) = "м" Тогда
		ПолеПрописи4наУкраинском = "Мужской";
	ИначеЕсли НРег(Род) = "ж" Тогда
		ПолеПрописи4наУкраинском = "Женский";
	ИначеЕсли НРег(Род) = "с" Тогда
		ПолеПрописи4наУкраинском = "Средний";
	КонецЕсли;
	
	ПолеПрописи5наУкраинском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 5));
	ПолеПрописи6наУкраинском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 6));
	ПолеПрописи7наУкраинском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 7));
	
	Род = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 8));
	
	Если	  НРег(Род = "м") Тогда
		ПолеПрописи8наУкраинском = "Мужской";
	ИначеЕсли НРег(Род = "ж") Тогда
		ПолеПрописи8наУкраинском = "Женский";
	ИначеЕсли НРег(Род = "с") Тогда
		ПолеПрописи8наУкраинском = "Средний";
	КонецЕсли;
	
	ДлинаДробнойЧастиУкр     = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 9));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваЭлементовГруппыЗависимойВалюты(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Объект.ЗагружаетсяИзИнтернета Тогда
		Элементы.ГруппаЗависимыйКурсВалют.Доступность = Ложь;
		ФлагПараметровЗависимогоКурса = Ложь;
	Иначе
		Элементы.ГруппаЗависимыйКурсВалют.Доступность = Истина;
		ФлагПараметровЗависимогоКурса = Форма.ЗависимыйКурсВалют;
	КонецЕсли;
	
	Элементы.ОсновнаяВалюта.АвтоОтметкаНезаполненного = ФлагПараметровЗависимогоКурса;
	Элементы.Наценка.АвтоОтметкаНезаполненного = ФлагПараметровЗависимогоКурса;
	Элементы.ОсновнаяВалюта.Доступность = ФлагПараметровЗависимогоКурса;
	Элементы.Наценка.Доступность = ФлагПараметровЗависимогоКурса;
	Если НЕ ФлагПараметровЗависимогоКурса Тогда
		Элементы.ОсновнаяВалюта.ОтметкаНезаполненного = ФлагПараметровЗависимогоКурса;
		Элементы.Наценка.ОтметкаНезаполненного = ФлагПараметровЗависимогоКурса;
	КонецЕсли
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСклоненияПараметровПрописи(Форма)
	
	// Склонение заголовков параметров прописи
	
	Элементы = Форма.Элементы;
	
	Если Форма.ПолеПрописи4наРусском = "Женский" Тогда
		Элементы.ПолеПрописи1наРусском.Заголовок = НСтр("ru='Одна';uk='Одна'");
		Элементы.ПолеПрописи2наРусском.Заголовок = НСтр("ru='Две';uk='Дві'");
	ИначеЕсли Форма.ПолеПрописи4наРусском = "Мужской" Тогда
		Элементы.ПолеПрописи1наРусском.Заголовок = НСтр("ru='Один';uk='Один'");
		Элементы.ПолеПрописи2наРусском.Заголовок = НСтр("ru='Два';uk='Два'");
	Иначе
		Элементы.ПолеПрописи1наРусском.Заголовок = НСтр("ru='Одно';uk='Одне'");
		Элементы.ПолеПрописи2наРусском.Заголовок = НСтр("ru='Два';uk='Два'");
	КонецЕсли;
	
	Если Форма.ПолеПрописи8наРусском = "Женский" Тогда
		Элементы.ПолеПрописи5наРусском.Заголовок = НСтр("ru='Одна';uk='Одна'");
		Элементы.ПолеПрописи6наРусском.Заголовок = НСтр("ru='Две';uk='Дві'");
	ИначеЕсли Форма.ПолеПрописи8наРусском = "Мужской" Тогда
		Элементы.ПолеПрописи5наРусском.Заголовок = НСтр("ru='Один';uk='Один'");
		Элементы.ПолеПрописи6наРусском.Заголовок = НСтр("ru='Два';uk='Два'");
	Иначе
		Элементы.ПолеПрописи5наРусском.Заголовок = НСтр("ru='Одно';uk='Одне'");
		Элементы.ПолеПрописи6наРусском.Заголовок = НСтр("ru='Два';uk='Два'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСклоненияПараметровПрописиУкр(Форма)
	
	// Склонение заголовков параметров прописи
	
	Элементы = Форма.Элементы;
	
	// Украинский
	Если Форма.ПолеПрописи4наУкраинском = "Женский" Тогда
		Элементы.ПолеПрописи1наУкраинском.Заголовок = "одна";
		Элементы.ПолеПрописи2наУкраинском.Заголовок = "дві";
	ИначеЕсли Форма.ПолеПрописи4наУкраинском = "Мужской" Тогда
		Элементы.ПолеПрописи1наУкраинском.Заголовок = "один";
		Элементы.ПолеПрописи2наУкраинском.Заголовок = "два";
	Иначе
		Элементы.ПолеПрописи1наУкраинском.Заголовок = "одно";
		Элементы.ПолеПрописи2наУкраинском.Заголовок = "два";
	КонецЕсли;
	
	Если Форма.ПолеПрописи8наУкраинском = "Женский" Тогда
		Элементы.ПолеПрописи5наУкраинском.Заголовок = "одна";
		Элементы.ПолеПрописи6наУкраинском.Заголовок = "дві";
	ИначеЕсли Форма.ПолеПрописи8наУкраинском = "Мужской" Тогда
		Элементы.ПолеПрописи5наУкраинском.Заголовок = "один";
		Элементы.ПолеПрописи6наУкраинском.Заголовок = "два";
	Иначе
		Элементы.ПолеПрописи5наУкраинском.Заголовок = "одне";
		Элементы.ПолеПрописи6наУкраинском.Заголовок = "два";
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьДанныеВыбораПодчиненнойВалюты(ДанныеВыбора, Ссылка)
	
	// Подготавливает список выбора для подчиненной валюты таким образом,
	// что бы в список не попала сама подчиненная валюта
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ Ссылка, НаименованиеПолное
	               |ИЗ
	               |	Справочник.Валюты
	               |ГДЕ
	               |	Ссылка <> &Ссылка
	               |И
	               |	ОсновнаяВалюта  = Значение(Справочник.Валюты.ПустаяСсылка)
	               |УПОРЯДОЧИТЬ ПО НаименованиеПолное";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.НаименованиеПолное);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка)
	
	// Вспомогательная функция управлением вводом
	
	Для Каждого ЭлементВыбора Из Элемент.СписокВыбора Цикл
		Если ВРег(Текст) = ВРег(Лев(ЭлементВыбора.Представление, СтрДлина(Текст))) Тогда
			Результат = Новый СписокЗначений;
			Результат.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
			СтандартнаяОбработка = Ложь;
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Функция ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка)
	
	// Вспомогательная функция управлением вводом
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого ЭлементВыбора Из Элемент.СписокВыбора Цикл
		Если ВРег(Текст) = ВРег(ЭлементВыбора.Представление) Тогда
			СтандартнаяОбработка = Истина;
		ИначеЕсли ВРег(Текст) = ВРег(Лев(ЭлементВыбора.Представление, СтрДлина(Текст))) Тогда
			СтандартнаяОбработка = Ложь;
			Результат = Новый СписокЗначений;
			Результат.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции


